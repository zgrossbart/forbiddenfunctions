defaultTasks 'build', 'jar', 'distZip2'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: 'maven'

mainClassName = 'com.grossbart.forbiddenfunction.FFRunner'
applicationName = 'forbiddenfunction'

/*
 * Now we define our application
 */
group = 'com.grossbart'
version = '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

/*
 * We specify our dependencies
 */
dependencies {
  compile group: 'com.google.javascript', name: 'closure-compiler', version: 'v20141215'
  compile group: 'args4j', name: 'args4j', version: '2.0.29'
  compile group: 'commons-io', name: 'commons-io', version: '2.1'
  testCompile group: 'junit', name: 'junit', version: '4.+'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.grossbart.forbiddenfunction.FFRunner'
    }
}

jar.doLast {
    tasks.install.execute()
}

/*
 * This task builds a single zip file containing our JAR, all
 * the JARs our JAR depends on, and some scripts to run the program.
 * It works just like the distZip from the Application plugin, but
 * it adds our license and README file to the root of the distribution
 * zip file.
 */
task distZip2(dependsOn: installApp, type: Zip) {
    from(jar.outputs.files) {
        into('lib')
    }
    
    from(project.configurations.runtime) {
        into('lib')
    }
    
    from(startScripts.outputs.files) {
        into('bin')
    }

    from('.') {
        include 'LICENSE.txt'
        include 'README.md'
    }
}
